if (project.hasProperty("ossrhUser") && project.hasProperty("ossrhPassword")) {

    def keyFile = new File(project.buildDir, 'key.pgp')
    keyFile << project.signKey
    project.ext.set("signing.secretKeyRingFile", keyFile.getPath())
    gradle.buildFinished { keyFile.delete() }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
    }

    subprojects {
        apply plugin: 'maven'
        apply plugin: 'signing'

        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description 'Data definition and queries Kotlin DSL over Xodus'
                        packaging 'jar'
                        url 'https://github.com/JetBrains/xodus-dnq'

                        scm {
                            connection 'scm:git:https://github.com/JetBrains/xodus-dnq.git'
                            developerConnection 'scm:git@github.com:JetBrains/xodus-dnq.git'
                            url 'https://github.com/JetBrains/xodus-dnq'
                        }

                        licenses {
                            license {
                                name 'Apache 2.0'
                                url 'http://www.apache.org/licenses/'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'JetBrains'
                                name 'JetBrains Team'
                                organization 'JetBrains s.r.o'
                                organizationUrl 'http://www.jetbrains.com'
                            }
                        }
                    }
                }
            }
        }

        task sourceJar(type: Jar) {
            classifier = 'sources'
            duplicatesStrategy 'exclude'
            includeEmptyDirs false
            from project.sourceSets.main.java
            from project.sourceSets.main.kotlin
        }

        // this is a stub javadoc until we investigate and fix the following docklet error somehow:
        // java.lang.ClassCastException: com.sun.tools.javac.code.Type$AnnotatedType cannot be cast to com.sun.tools.javac.code.Type$ClassType
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from "${rootProject.projectDir}/README.MD"
        }

        artifacts {
            archives jar, sourceJar, javadocJar
        }
    }
}