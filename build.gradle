buildscript {
    ext.kotlin_version = '1.1.4'
    ext.exodus_version = '2762-SNAPSHOT'
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
    id "org.jetbrains.kotlin.jvm" version '1.1.4'
    id "idea"
}

group = 'kotlinx.dnq'
version = (project.hasProperty("teamcity")) ? project.teamcity["build.number"] : 'SNAPSHOT-1.0'

idea {
    project {
        jdkName = '1.8'
        vcs = 'Git'
    }
    module {
        downloadSources = true
    }
}

subprojects {
    apply plugin: 'license'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'

    sourceCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/xodus' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.4"
        compile 'io.github.microutils:kotlin-logging:1.4.1'

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.21'
    }


    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2006
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
        include "**/*.kt"
        include "**/*.java"
        mapping {
            kt = 'JAVADOC_STYLE'
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging.showStandardStreams = true
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    artifacts {
        archives jar, sourceJar
    }
}
