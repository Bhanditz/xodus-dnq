buildscript {
    ext.kotlin_version = '1.1.4'
    ext.exodus_version = '2762-SNAPSHOT'

    repositories { jcenter() }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

group = 'kotlinx.dnq'
version = (project.hasProperty("teamcity")) ? project.teamcity["build.number"] : 'SNAPSHOT-1.0'

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

defaultTasks 'assemble'

apply plugin: 'idea'

idea {
    project {
        jdkName = '1.8'
        vcs = 'Git'
    }
    module {
        downloadSources = true
    }
}

subprojects {
    apply plugin: 'license'
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'

    def kotlinUsed = project.sourceSets.main.kotlin.sourceDirectories.singleFile.exists()

    sourceCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    group = rootProject.group
    version = rootProject.version

    license {
        header rootProject.file('license/copyright.ftl')
        strictCheck true
        ext.inceptionYear = 2006
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.owner = 'JetBrains s.r.o.'
        include "**/*.kt"
        include "**/*.java"
        mapping {
            kt = 'JAVADOC_STYLE'
        }
    }

    repositories {
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/xodus' }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.21'
    }

    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging.showStandardStreams = true
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
    }

    // configuring projects with Kotlin sources
    if (kotlinUsed) {
        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            compile 'io.github.microutils:kotlin-logging:1.4.1'
        }

        sourceJar {
            from project.sourceSets.main.kotlin
        }
    }

    artifacts {
        archives jar, sourceJar
    }
}
